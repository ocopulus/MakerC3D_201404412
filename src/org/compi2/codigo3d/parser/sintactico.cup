package org.compi2.codigo3d.parser;

import java_cup.runtime.Symbol;

//PARSER PARA EL ARCHIVO DE ESCENARIOS
parser code
{://Codigo visible

    public void syntax_error(Symbol s){
        System.err.println(s);
    }

    public void unrecovered_syntax_error(Symbol s) throws java.lang.Exception{ 
        System.err.println("T.T");
        System.err.println(s);
    }

:}

action code
{://Codigo de acciones
      
:}

terminal String ID, ENTERO, DECIMAL;
terminal TokenC3D MAS, MENOS, MULT, DIV, POT, PAR_IZQ, PAR_DER;
terminal TokenC3D MENOR,MAYOR,MENOR_IGUAL,MAYOR_IGUAL,IGUAL,DIFERENTE,AND,OR,NOT;
nonterminal NodoC3D inicial, expresion;

precedence left OR;
precedence left AND;
precedence left NOT;
precedence left MENOR,MAYOR,MENOR_IGUAL,MAYOR_IGUAL,IGUAL,DIFERENTE;
precedence left MAS, MENOS;
precedence left MULT, DIV; 
precedence right POT; 

start with inicial;

inicial     ::= {: ControlC3D.reiniciar(); :} expresion:exp 
                    {: 
                        RESULT = exp;
                        if(exp.getEtv() == null && exp.getEtf() == null){
                            ControlC3D.agregarC3D("//Ãšltimo valor: " + exp.getCad());
                        }else{
                            ControlC3D.agregarC3D("//Etiqueta(s) de verdadero: " + exp.getEtv() + "\n" + "//Etiqueta(s) de falso: " + exp.getEtf());
                        }
                    :};

expresion   ::= expresion:izq MAS expresion:der
                    {: 
                        String temp = ControlC3D.generaTemp();
                        String c3d = temp + " = " + izq.getCad() + " + " + der.getCad() + ";\n";
                        ControlC3D.agregarC3D(c3d);
                        RESULT = new NodoC3D(temp); 
                    :}
            |   expresion:izq MENOS expresion:der
                    {: 
                        String temp = ControlC3D.generaTemp();
                        String c3d = temp + " = " + izq.getCad() + " - " + der.getCad() + ";\n";
                        ControlC3D.agregarC3D(c3d);
                        RESULT = new NodoC3D(temp); 
                    :}
            |   expresion:izq MULT expresion:der
                    {: 
                        String temp = ControlC3D.generaTemp();
                        String c3d = temp + " = " + izq.getCad() + " * " + der.getCad() + ";\n";
                        ControlC3D.agregarC3D(c3d);
                        RESULT = new NodoC3D(temp); 
                    :}
            |   expresion:izq DIV expresion:der
                    {: 
                        String temp = ControlC3D.generaTemp();
                        String c3d = temp + " = " + izq.getCad() + " / " + der.getCad() + ";\n";
                        ControlC3D.agregarC3D(c3d);
                        RESULT = new NodoC3D(temp); 
                    :}
            |   expresion:izq POT expresion:der
                    {: 
                        String temp = ControlC3D.generaTemp();
                        String c3d = temp + " = " + izq.getCad() + " ^ " + der.getCad() + ";\n";
                        ControlC3D.agregarC3D(c3d);
                        RESULT = new NodoC3D(temp); 
                    :}
            |   expresion:izq MENOR expresion:der 
                    {:
                        String etqv = ControlC3D.generaETQ();
                        String etqf = ControlC3D.generaETQ();
                        String temp = ControlC3D.generaTemp();
                        String c3d = "if " + izq.getCad() + " < " + der.getCad() + "  goto " + etqv + ";\ngoto " + etqf + ";\n";
                        ControlC3D.agregarC3D(c3d);
                        RESULT = new NodoC3D(temp, etqv, etqf); 
                    :}
            |   expresion:izq MAYOR expresion:der 
                    {:
                        String etqv = ControlC3D.generaETQ();
                        String etqf = ControlC3D.generaETQ();
                        String temp = ControlC3D.generaTemp();
                        String c3d = "if " + izq.getCad() + " > " + der.getCad() + "  goto " + etqv + ";\ngoto " + etqf + ";\n";
                        ControlC3D.agregarC3D(c3d);
                        RESULT = new NodoC3D(temp, etqv, etqf); 
                    :}
            |   expresion:izq MENOR_IGUAL expresion:der 
                    {:
                        String etqv = ControlC3D.generaETQ();
                        String etqf = ControlC3D.generaETQ();
                        String temp = ControlC3D.generaTemp();
                        String c3d = "if " + izq.getCad() + " <= " + der.getCad() + "  goto " + etqv + ";\ngoto " + etqf + ";\n";
                        ControlC3D.agregarC3D(c3d);
                        RESULT = new NodoC3D(temp, etqv, etqf); 
                    :}
            |   expresion:izq MAYOR_IGUAL expresion:der 
                    {:
                        String etqv = ControlC3D.generaETQ();
                        String etqf = ControlC3D.generaETQ();
                        String temp = ControlC3D.generaTemp();
                        String c3d = "if " + izq.getCad() + " >= " + der.getCad() + "  goto " + etqv + ";\ngoto " + etqf + ";\n";
                        ControlC3D.agregarC3D(c3d);
                        RESULT = new NodoC3D(temp, etqv, etqf); 
                    :}
            |   expresion:izq IGUAL expresion:der 
                    {:
                        String etqv = ControlC3D.generaETQ();
                        String etqf = ControlC3D.generaETQ();
                        String temp = ControlC3D.generaTemp();
                        String c3d = "if " + izq.getCad() + " == " + der.getCad() + "  goto " + etqv + ";\ngoto " + etqf + ";\n";
                        ControlC3D.agregarC3D(c3d);
                        RESULT = new NodoC3D(temp, etqv, etqf); 
                    :}
            |   expresion:izq DIFERENTE expresion:der 
                    {:
                        String etqv = ControlC3D.generaETQ();
                        String etqf = ControlC3D.generaETQ();
                        String temp = ControlC3D.generaTemp();
                        String c3d = "if " + izq.getCad() + " != " + der.getCad() + "  goto " + etqv + ";\ngoto " + etqf + ";\n";
                        ControlC3D.agregarC3D(c3d);
                        RESULT = new NodoC3D(temp, etqv, etqf); 
                    :}
            |   expresion:izq AND {:ControlC3D.agregarC3D(izq.getEtv()+":\n");:} expresion:der 
                    {:
                        String etqv = der.getEtv();
                        String etqf = izq.getEtf() + "," + der.getEtf();
                        RESULT = new NodoC3D("", etqv, etqf); 
                    :}
            |   expresion:izq OR {:ControlC3D.agregarC3D(izq.getEtf()+":\n");:} expresion:der 
                    {:
                        String etqv = izq.getEtv() + "," + der.getEtv();
                        String etqf = der.getEtf();
                        RESULT = new NodoC3D("", etqv, etqf); 
                    :}
            |   NOT expresion:der 
                    {:
                        RESULT = new NodoC3D("", der.getEtf(), der.getEtv()); 
                    :}
            |   MENOS expresion:exp 
                    {: 
                        String temp = ControlC3D.generaTemp();
                        String c3d = temp + " = - " + exp.getCad() + ";\n";
                        ControlC3D.agregarC3D(c3d);
                        RESULT = new NodoC3D(temp); 
                    :}
            |   PAR_IZQ expresion:exp PAR_DER   
                    {: RESULT = exp; 
                        String etqv = exp.getEtv();
                        String etqf = exp.getEtf();
                    :}
            |   ID:id           
                    {: RESULT = new NodoC3D(id); :}
            |   ENTERO:entero   
                    {: RESULT = new NodoC3D(entero); :}
            |   DECIMAL:decimal 
                    {: RESULT = new NodoC3D(decimal); :}
            ;